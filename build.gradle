plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.0.20'
    id 'maven-publish'
}

group = 'me.hellosunghyun'
version = '0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

kotlin {
    jvmToolchain(17)
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0")
    implementation("com.google.code.gson:gson:2.11.0")
}

publishing {
    repositories {
        maven {
            name = "SoopChatSDK"
            url = uri("https://maven.pkg.github.com/hellosunghyun/SoopChatSDK")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        create("hellosunghyun", MavenPublication) {
            from(components["java"])
            groupId = 'me.hellosunghyun'
            artifactId = 'SoopChatSDK'
            version = '0.1-SNAPSHOT'

            pom {
                name.set('SoopChatSDK')
                description.set('Description of your project.')
                url.set('https://github.com/hellosunghyun/SoopChatSDK')

                licenses {
                    license {
                        name.set('The Apache License, Version 2.0')
                        url.set('http://www.apache.org/licenses/LICENSE-2.0.txt')
                    }
                }

                developers {
                    developer {
                        id.set('hellosunghyun')
                        name.set('Your Name')
                        email.set('your_email@example.com')
                    }
                }

                scm {
                    connection.set('scm:git:git://github.com/hellosunghyun/SoopChatSDK.git')
                    developerConnection.set('scm:git:ssh://github.com/hellosunghyun/SoopChatSDK.git')
                    url.set('https://github.com/hellosunghyun/SoopChatSDK')
                }
            }
        }
    }
}

test {
    useJUnitPlatform()
}
